import subprocess
import os

Import('env', 'arch', 'common', 'messaging', 'visionipc', 'replay_lib', 'cereal')

qt_env = env.Clone()

# Configure build directory
build_dir = Dir('#build').abspath
qt_env.VariantDir('build', '.', duplicate=0)
qt_env['QT_MOCFROMHDIR'] = os.path.join(build_dir, 'moc')
qt_env['QT_MOCFROMCXXDIR'] = os.path.join(build_dir, 'moc')

qt_env["CPPPATH"] += ["#"]
qt_modules = ["Widgets", "Gui", "Core", "Network", "Concurrent", "DBus", "Xml"]

qt_libs = []
if arch == "Darwin":
  brew_prefix = subprocess.check_output(['brew', '--prefix'], encoding='utf8').strip()
  qt_env['QTDIR'] = f"{brew_prefix}/opt/qt@5"
  qt_dirs = [
    os.path.join(qt_env['QTDIR'], "include"),
  ]
  qt_dirs += [f"{qt_env['QTDIR']}/include/Qt{m}" for m in qt_modules]
  qt_env["LINKFLAGS"] += ["-F" + os.path.join(qt_env['QTDIR'], "lib")]
  qt_env["FRAMEWORKS"] += [f"Qt{m}" for m in qt_modules] + ["OpenGL"]
  qt_env.AppendENVPath('PATH', os.path.join(qt_env['QTDIR'], "bin"))
else:
  qt_install_prefix = subprocess.check_output(['qmake', '-query', 'QT_INSTALL_PREFIX'], encoding='utf8').strip()
  qt_install_headers = subprocess.check_output(['qmake', '-query', 'QT_INSTALL_HEADERS'], encoding='utf8').strip()

  qt_env['QTDIR'] = qt_install_prefix
  qt_dirs = [
    f"{qt_install_headers}",
  ]

  qt_gui_path = os.path.join(qt_install_headers, "QtGui")
  qt_gui_dirs = [d for d in os.listdir(qt_gui_path) if os.path.isdir(os.path.join(qt_gui_path, d))]
  qt_dirs += [f"{qt_install_headers}/QtGui/{qt_gui_dirs[0]}/QtGui", ] if qt_gui_dirs else []
  qt_dirs += [f"{qt_install_headers}/Qt{m}" for m in qt_modules]

  qt_libs = [f"Qt5{m}" for m in qt_modules]
  if arch == "larch64":
    qt_libs += ["GLESv2", "wayland-client"]
    qt_env.PrependENVPath('PATH', Dir("#third_party/qt5/larch64/bin/").abspath)
  elif arch != "Darwin":
    qt_libs += ["GL"]
qt_env['QT3DIR'] = qt_env['QTDIR']
qt_env.Tool('qt3')

qt_env['CPPPATH'] += qt_dirs + ["#src", "#src/widgets", "#src/streams", "#src/dbc", "#src/chart", "#src/tools", "#src/utils"]
qt_flags = [
  "-D_REENTRANT",
  "-DQT_NO_DEBUG",
  "-DQT_WIDGETS_LIB",
  "-DQT_GUI_LIB",
  "-DQT_CORE_LIB",
  "-DQT_MESSAGELOGCONTEXT",
]
qt_env['CXXFLAGS'] += qt_flags
qt_env['LIBS'] = qt_libs

base_frameworks = qt_env['FRAMEWORKS']
base_libs = [common, messaging, cereal, visionipc, 'm', 'ssl', 'crypto', 'pthread'] + qt_env["LIBS"]

if arch == "Darwin":
  base_frameworks.append('OpenCL')
  base_frameworks.append('QtCharts')
  base_frameworks.append('QtSerialBus')
else:
  base_libs.append('OpenCL')
  base_libs.append('Qt5Charts')
  base_libs.append('Qt5SerialBus')

qt_libs = base_libs

cabana_env = qt_env.Clone()

# Set build output directory for object files
cabana_env.VariantDir('build', '.', duplicate=0)

cabana_libs = [cereal, messaging, visionipc, replay_lib, 'avutil', 'avcodec', 'avformat', 'swscale','bz2', 'zstd', 'curl', 'usb-1.0'] + qt_libs

# build assets
assets = "build/assets/assets.cc"
assets_src = "assets/assets.qrc"
cabana_env.Command(assets, assets_src, f"rcc $SOURCES -o $TARGET")
cabana_env.Depends(assets, Glob('assets/*', exclude=[assets, assets_src, "assets/assets.o"]))

cabana_lib = cabana_env.Library(
    "build/cabana_lib",
    [
        "build/mainwin.cc",
        "build/streams/socketcanstream.cc",
        "build/streams/pandastream.cc",
        "build/streams/devicestream.cc",
        "build/streams/livestream.cc",
        "build/streams/abstractstream.cc",
        "build/streams/replaystream.cc",
        "build/streams/candata.cc",
        "build/streams/panda.cc",
        "build/widgets/binaryview.cc",
        "build/widgets/historylog.cc",
        "build/widgets/videowidget.cc",
        "build/widgets/signalview.cc",
        "build/streams/routes.cc",
        "build/dbc/dbc.cc",
        "build/dbc/dbcfile.cc",
        "build/dbc/dbcmanager.cc",
        "build/utils/export.cc",
        "build/utils/util.cc",
        "build/utils/elidedlabel.cc",
        "build/utils/api.cc",
        "build/chart/chartswidget.cc",
        "build/chart/chart.cc",
        "build/chart/signalselector.cc",
        "build/chart/tiplabel.cc",
        "build/chart/sparkline.cc",
        "build/commands.cc",
        "build/widgets/messageswidget.cc",
        "build/widgets/streamselector.cc",
        "build/settings.cc",
        "build/widgets/cameraview.cc",
        "build/widgets/detailwidget.cc",
        "build/tools/findsimilarbits.cc",
        "build/tools/findsignal.cc",
        "build/tools/routeinfo.cc",
    ],
    LIBS=cabana_libs,
    FRAMEWORKS=base_frameworks,
)
cabana_env.Program('../cabana', ['build/main.cc', cabana_lib, assets], LIBS=cabana_libs, FRAMEWORKS=base_frameworks)
